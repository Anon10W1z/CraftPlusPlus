buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.2-SNAPSHOT'
    }
}

apply plugin: 'net.minecraftforge.gradle.forge'

version = "4.1.1"
def ENV = System.getenv()
if (ENV.DRONE_BUILD_NUMBER)
    version += "-build" + ENV.DRONE_BUILD_NUMBER

group = "io.github.anon10w1z.cpp"
archivesBaseName = "craft++"

sourceCompatibility = 1.8
targetCompatibility = 1.8

minecraft {
    version = "1.10.2-12.18.1.2011"
    runDir = "run"
    mappings = "snapshot_20160518"
}

jar {
    manifest {
        attributes 'FMLCorePlugin': 'io.github.anon10w1z.cpp.coremod.CppLoadingPlugin', 'FMLCorePluginContainsFMLMod': 'true'
    }
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archivesBaseName = "craft++"
    classifier = 'javadoc'
    from javadoc.destinationDir
}

tasks.build.dependsOn('javadocJar')

artifacts {
    archives javadocJar
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        expand 'version': project.version, 'mcversion': project.minecraft.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

apply plugin: 'idea'
idea {
    module {
        inheritOutputDirs = true
    }
} //for assets to work with IntelliJ